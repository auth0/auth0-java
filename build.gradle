plugins {
    id 'java'
    id 'jacoco'
    id 'com.auth0.gradle.oss-library.java'
    id "com.jfrog.bintray" version "1.8.4"
}

repositories {
    jcenter()
}

group = 'com.auth0'
logger.lifecycle("Using version ${version} for ${name}")

oss {
    name 'auth0'
    repository 'auth0-java'
    organization 'auth0'
    description 'Java client library for the Auth0 platform.'

    developers {
        auth0 {
            displayName = 'Auth0'
            email = 'oss@auth0.com'
        }
        lbalmaceda {
            displayName = 'Luciano Balmaceda'
            email = 'luciano.balmaceda@auth0.com'
        }
    }
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }
}

compileJava {
    sourceCompatibility '1.8'
    targetCompatibility '1.8'

    options.compilerArgs << "-Xlint:deprecation" << "-Xlint:unchecked" << "-Werror"
}

compileTestJava {
    options.compilerArgs << "-Xlint:deprecation" << "-Werror"
    }

test {
    testLogging {
        events "skipped", "failed"
        exceptionFormat "short"
    }
}

dependencies {
    implementation 'com.squareup.okhttp3:okhttp:3.14.9'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.14.9'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.11.3'
    implementation 'commons-codec:commons-codec:1.15'
    implementation 'com.auth0:java-jwt:3.11.0'

    testImplementation 'org.bouncycastle:bcprov-jdk15on:1.65'
    testImplementation 'org.mockito:mockito-core:2.28.2'
    testImplementation 'com.squareup.okhttp3:mockwebserver:3.14.9'
    testImplementation 'org.hamcrest:hamcrest-core:1.3'
    testImplementation 'org.hamcrest:hamcrest-library:1.3'
    testImplementation 'junit:junit:4.13.1'
}
