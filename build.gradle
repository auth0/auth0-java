group = 'com.auth0'

apply plugin: 'com.auth0.gradle.oss-library.java'
apply plugin: 'jacoco'

logger.lifecycle("Using version ${version} for ${name}")

oss {
    name 'auth0'
    repository 'auth0-java'
    organization 'auth0'
    description 'Java client library for the Auth0 platform.'

    developers {
        auth0 {
            displayName = 'Auth0'
            email = 'oss@auth0.com'
        }
        lbalmaceda {
            displayName = 'Luciano Balmaceda'
            email = 'luciano.balmaceda@auth0.com'
        }
    }
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

compileJava {
    sourceCompatibility '1.7'
    targetCompatibility '1.7'
}

buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7'
        classpath "gradle.plugin.com.auth0.gradle:oss-library:0.6.0"
    }
}

repositories {
    mavenCentral()
}

test {
    testLogging {
        events "skipped", "failed"
        exceptionFormat "short"
    }
}

dependencies {
    compile 'com.squareup.okhttp3:okhttp:3.7.0'
    compile 'com.squareup.okhttp3:logging-interceptor:3.7.0'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.8.5'
    compile 'commons-codec:commons-codec:1.10'

    testCompile 'org.mockito:mockito-core:2.5.4'
    testCompile 'com.squareup.okhttp3:mockwebserver:3.7.0'
    testCompile 'org.hamcrest:hamcrest-core:1.3'
    testCompile 'org.hamcrest:hamcrest-library:1.3'
    testCompile 'junit:junit:4.11'
}
